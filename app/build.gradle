apply plugin: 'com.android.application'

def gitVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1
    }
}

def gitVersionName = { ->
    try {
        def stdout =  new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.nightscout.android"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode gitVersionCode()
        versionName gitVersionName()
        useLibrary 'org.apache.http.legacy'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

}

dependencies {
    implementation files('libs/logback-android-1.1.1-3.jar')
    implementation files('libs/mongo-java-driver-3.0.2.jar')
    implementation files('libs/physicaloidlibrary.jar')
    implementation files('libs/slf4j-api-1.7.2.jar')

}
